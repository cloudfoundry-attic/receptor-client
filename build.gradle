description = 'Receptor Client'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply from:   "${rootProject.projectDir}/publish-maven.gradle"

ext {
    linkHomepage = 'https://github.com/cloudfoundry-incubator/receptor-client'
    linkCi = 'https://build.spring.io/browse/RC-RC'
    linkIssue = 'https://github.com/cloudfoundry-incubator/receptor-client/issues'
    linkScmUrl = 'https://github.com/cloudfoundry-incubator/receptor-client'
    linkScmConnection = 'scm:git:git://github.com/cloudfoundry-incubator/receptor-client.git'
    linkScmDevConnection = 'scm:git:ssh://git@github.com:cloudfoundry-incubator/receptor-client.git'
}

group = 'org.cloudfoundry.receptor'

jar {
    baseName = 'receptor-client'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile("com.fasterxml.jackson.core:jackson-databind:2.6.1")
    compile("org.springframework:spring-web:4.2.1.RELEASE")
    testCompile('org.mockito:mockito-core:1.10.19')
    testCompile("junit:junit:4.12")
}

test {
    useJUnit {
        excludeCategories 'org.cloudfoundry.receptor.client.test.IntegrationTest'
    }
}

task integrationTest(type: Test, dependsOn: test) {
    description 'Runs all integration tests'
    group = "Verification"

    useJUnit {
        includeCategories 'org.cloudfoundry.receptor.client.IntegrationTest'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            credentials {
                username "$System.env.RC_REP_USER"
                password "$System.env.RC_REP_PASSWORD"
            }
            url "$System.env.RC_REPOSITORY" //i.e. http://repo.spring.io/libs-snapshot-local
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId 'receptor-client'
            from components.java

            artifact sourcesJar {
                artifactId 'receptor-client'
                classifier "sources"
            }

            artifact javadocJar {
                artifactId 'receptor-client'
                classifier "javadoc"
            }
        }
    }
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '2.5'
    distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
